apply plugin: "com.android.application"
apply plugin: "kotlin-android"

def keystorePropertiesFile = rootProject.file("keystore.properties")
android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "msfjarvis.wirelessadb"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 8
        versionName "5.3"
    }
    if (keystorePropertiesFile.exists()) {
        println "Found signature properties file. Build will be signed."
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        signingConfigs {
            release {
                storeFile rootProject.file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles "proguard-android-optimize.txt", "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation "eu.chainfire:libsuperuser:1.1.0.202004101746"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

repositories {
    mavenCentral()
}
